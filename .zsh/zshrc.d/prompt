# ~/zsh/zshrc.d/prompt
##############################################################################
#
# Prompt based upon work by Phil Gregory (http://aperiodic.net/phil/prompt/).
# Moved date/time to right side of prompt.  Eliminated date.  Added command
# history.  Converted to unicode (removed termcap line drawing).  Adjusted
# colors to match solarized color theme.
#
##############################################################################

function precmd {
	local TERMWIDTH
	(( TERMWIDTH = ${COLUMNS} - 1 ))

	PR_FILLBAR=""
	PR_PWDLEN=""

	local promptsize=${#${(%):---(%n@%m:%l)---()--}}
	local pwdsize=${#${(%):-%~}}
	if [[ "$promptsize + $pwdsize" -gt $TERMWIDTH ]]; then
		((PR_PWDLEN=$TERMWIDTH - $promptsize))
	else
		PR_FILLBAR="\${(l.(($TERMWIDTH - ($promptsize + $pwdsize)))..${PR_HBAR}.)}"
	fi
}

setopt extended_glob

preexec () {
	if [[ "$TERM" == "screen" ]]; then
		local CMD=${1[(wr)^(*=*|sudo|-*)]}
		echo -n "\ek$CMD\e\\"
	fi
}

setprompt () {
	# Need this so the prompt will work.
	setopt prompt_subst

	# Load color information from termcap.
	autoload colors zsh/terminfo
	if [[ "$terminfo[colors]" -ge 8 ]]; then
		colors
	fi

	# Create color variables based on solarized color names.
	PR_BASE03="%{$fg_bold[black]%}"
	PR_BASE02="%{$fg_no_bold[black]%}"
	PR_BASE01="%{$fg_bold[green]%}"
	PR_BASE00="%{$fg_bold[yellow]%}"
	PR_BASE0="%{$fg_bold[blue]%}"
	PR_BASE1="%{$fg_bold[cyan]%}"
	PR_BASE2="%{$fg_no_bold[white]%}"
	PR_BASE3="%{$fg_bold[white]%}"
	PR_YELLOW="%{$fg_no_bold[yellow]%}"
	PR_ORANGE="%{$fg_no_bold[red]%}"
	PR_RED="%{$fg_bold[red]%}"
	PR_MAGENTA="%{$fg_no_bold[magenta]%}"
	PR_VIOLET="%{$fg_bold[magenta]%}"
	PR_BLUE="%{$fg_no_bold[blue]%}"
	PR_CYAN="%{$fg_no_bold[cyan]%}"
	PR_GREEN="%{$fg_no_bold[green]%}"

	PR_NO_COLOUR="%{$terminfo[sgr0]%}"

	# See if we can use unicode characters to look nicer.
	PR_HBAR=$(echo '\u2500')
	PR_ULCORNER=$(echo '\u250C')
	PR_LLCORNER=$(echo '\u2514')
	PR_LRCORNER=$(echo '\u2518')
	PR_URCORNER=$(echo '\u2510')
	#PR_HBAR="-"
	#PR_ULCORNER="+"
	#PR_LLCORNER="+"
	#PR_LRCORNER="+"
	#PR_URCORNER="+"

	# Decide if we need to set titlebar text.
	case $TERM in
		xterm*|rxvt)
			PR_TITLEBAR=$'%{\e]0;%(!.-=*[ROOT]*=- | .)%n@%m:%~ | ${COLUMNS}x${LINES} | %y\a%}'
			;;
		screen)
			PR_TITLEBAR=$'%{\e_screen \005 (\005t) | %(!.-=[ROOT]=- | .)%n@%m:%~ | ${COLUMNS}x${LINES} | %y\e\\%}'
			;;
		*)
			PR_TITLEBAR=''
			;;
	esac

	# Decide whether to set a screen title
	if [[ "$TERM" == "screen" ]]; then
		PR_STITLE=$'%{\ekzsh\e\\%}'
	else
		PR_STITLE=''
	fi

	# Finally, the prompt.
	PROMPT='$PR_SET_CHARSET$PR_STITLE${(e)PR_TITLEBAR}\
$PR_BASE1$PR_ULCORNER$PR_HBAR(\
$PR_BLUE%(!.%SROOT%s.%n)@%m:%l\
$PR_BASE1)$PR_HBAR$PR_HBAR${(e)PR_FILLBAR}$PR_HBAR(\
$PR_BLUE%$PR_PWDLEN<...<%~%<<\
$PR_BASE1)$PR_HBAR$PR_URCORNER\

$PR_BASE1$PR_LLCORNER$PR_HBAR(\
%(?..$PR_RED%?$PR_BLUE:)\
$PR_GREEN%!\
$PR_BLUE:%(!.$PR_RED.$PR_BASE01)%#$PR_BASE1)$PR_HBAR\
$PR_HBAR\
$PR_NO_COLOUR '

	RPROMPT=' $PR_BASE1$PR_HBAR$PR_HBAR\
($PR_GREEN%D{%H:%M}$PR_BASE1)$PR_HBAR$PR_LRCORNER$PR_NO_COLOUR'

	PS2='$PR_BASE1$PR_HBAR\
$PR_HBAR($PR_YELLOW%_$PR_BASE1)$PR_HBAR\
$PR_HBAR$PR_NO_COLOUR '
}

setprompt

##############################################################################
# vim: syntax=zsh
##############################################################################
